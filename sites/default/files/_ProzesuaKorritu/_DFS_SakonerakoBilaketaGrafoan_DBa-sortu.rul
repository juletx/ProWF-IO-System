;Workflow-a exekutatzerako orduan hurrengoa kontuan izan behar da:

;- Nodoen izenak beti hurrengo egitura izango dute:

;	"PROZESU_MOTA NODO_MOTA KODE_MULTZOA NODOAREN_IZENA | {BERTSIOA edo ROLA edo KARDINALITATEA edo URLa}"
	
;	- PROZESU_MOTA: egungo prozesu edo azpi-prozesuaren mota da --> {Methodology, Phase, Activity, Task, Artifact}
	
;	- NODO_MOTA: nodoaren mota da
;			- Start: prozesu edo azpi-prozesuaren hasiera.
;				- "Methodology Start 1 0 0 0 0 0 0 OpenUp Lifecycle | Alpha"    Start nodoen salbuespena
;				- "Phase Start 1 1 0 0 0 0 0 Inception |"						Start nodoen formatua
;			- End: prozesu edo azpi-prozesuaren amaiera.
;				- "Methodology End 1 0 0 0 0 0 0 O OpenUp Lifecycle | Alpha"	End salbuespena
;				- "Phase End 1 1 0 0 0 0 0 Inception |"							End nodoen formatua
;			- Phase: nodoak "Phase" motako azpi-prozesua du.
;				- "Phase Activity 1 1 4 0 0 0 0 Agree on the Technical Approach |"
;			- Activity: nodoak "Activity" motako azpi-prozesua du.
;				- "Activity Task 1 1 4 1 3 0 0 Outline the Architecture | Architect"
;			- Task: nodoak "Task" motako azpi-prozesua du.
;				- "Task Artifact 1 1 4 1 3 1 0 Architecture Notebook |" 
;			- Artifact: nodoak "Artifact" motako azpi-prozesua du. 
;				- Section: nodoa idatzi beharreko sekzioa da. 
;					- "Artifact Section 1 1 1 1 1 14 3 Stakeholder Descriptions |"
;				- SectionWT: (With Template) nodoa idatzi beharreko sekzioa da eta txantiloia du. 
;					- "Artifact SectionWT 1 1 4 1 3 1 7 Key abstractions | Templates/ArchitectureNotebook/KeyAbstractions.txt"
;				- UploadGraph: nodoa igo beharreko grafoa da, hau bakarrik "Use-Case Model" artefaktuan erabiliko da.
;			- Join: elkartze erromboa da.
;				- "Phase Join 1 1 0 0 0 0 0 End access | 3 1"
;			- Question: galdera da.
;				- "Methodology Question 1 2 0 0 0 0 0 Elaboration |"
;			- Decision: erabaki erromboa da.
;				- "Methodology Decision 1 2 0 0 0 0 0 Elaboration |"
			
;	- KODE_MULTZOA: informazioa datu-basean gordetzeko erabiliko diren identifikatzaileak (zenbakiak).	
;			- method_id: metodologia identifikatzeko zenbakia.
;			- phase_id: fasea identifikatzeko zenbakia.
;			- activity_id: aktibitatea identifikatzeko zenbakia.
;			- task_id: jarduera identifikatzeko zenbakia.
;			- role_id: rola identifikatzeko zenbakia.
;			- artifact_id: artefaktua identifikatzeko zenbakia.
;			- section_number: sekzioaren zenbakia (float)
			
;	- NODOAREN_IZENA: nodoaren izena (karaktere kate bat baino gehiago izan daiteke).
	
;	- BERTSIOA: metodologiaren START eta END nodoetan agertuko da bakarrik, metodologiaren bertsioa adierazteko
	
;	- ROLA: jardueren (task) kasuan agertuko da bakarrik, rolaren izena adierazteko
	
;	- KARDINALITATEA: JOIN motako nodoen kasuan agertuko da, bere kardinalitatea adieraziko da, adibidez: bi sarrera, irteera bat --> 2 1
	
;	- URLa: SectionWT motako nodoetan agertuko da bakarrik, sekzioaren txantiloia non gordetzen den adierazteko (path)
	

;- OpenUp-eko Inception fasea osorik inplementantu da eta Elaboration faseko hasiera, fase aldaketa ikusi ahal izateko. 


;- Exekutatzeratzean datu-basea ezabatu eta berria sortzeaz aparte, .csv fitxategi bat bueltatuko da, assesment atributuetan 10 bat ez duten artefaktuekin.
;Bi assesment atributu daude datu-basean editorearen eta errebisatzailearena (Kalitate Arduraduna).
;CSV fitxategia -> phase_id;activity_id;task_id;role_id;artifact_id;mota (Write | UploadGraph);editorearen_nota (0-10);errebisatzailearen_nota (0-10)
;Adibidez: 1;1;2;4;14;Write;10;8


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;



	

; Objketu izenetik grafo izena atera
(deffunction grafoIzena (?objketuIzenaKortxeteekin)             ;; [openUp_lifecycle_edge_5]
(bind  ?objketuIzena  (str-cat ?objketuIzenaKortxeteekin))

(if (str-index  "_graph" ?objketuIzena)
	then  
		(return (str-cat (sub-string 1 (- (str-index  "_graph" ?objketuIzena) 1) ?objketuIzena)))
	else
		(if (str-index  "_edge" ?objketuIzena)
				then
					(return (str-cat (sub-string 1 (- (str-index  "_edge" ?objketuIzena) 1) ?objketuIzena)))
				else
					(if (str-index  "_node" ?objketuIzena)
							then
								(return (str-cat (sub-string 1 (- (str-index  "_node" ?objketuIzena) 1) ?objketuIzena)))
							else
								(if (str-index  "_data" ?objketuIzena)
										then
											(return (str-cat (sub-string 1 (- (str-index  "_data" ?objketuIzena) 1) ?objketuIzena)))))))
	)
								
; (str-cat (sub-string 2 (- (str-index  "_edge" [openUp_lifecycle_edge_5]) 1) [openUp_lifecycle_edge_5]))     	=> "penUp_lifecycle"
; (str-cat (sub-string 2 (- (str-index  "_edge" "[openUp_lifecycle_edge_5]") 1) "[openUp_lifecycle_edge_5]"))  => "openUp_lifecycle"
; (str-cat (sub-string 2 (- (str-index  "_edge" "openUp_lifecycle_edge_5") 1) "openUp_lifecycle_edge_5"))  => "penUp_lifecycle"
; (str-cat (sub-string 1 (- (str-index  "_edge" "openUp_lifecycle_edge_5") 1) "openUp_lifecycle_edge_5"))  => "openUp_lifecycle"

(deffunction WorkflowBerekoObjektuIzenak (?objketuIzenaKortxeteekin1 ?objketuIzenaKortxeteekin2 )	
	(return (eq (grafoIzena ?objketuIzenaKortxeteekin1)
				(grafoIzena ?objketuIzenaKortxeteekin2)))
)



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;	
	
	
(deffunction DeiarenAzpiprozesua (?erpina)
;	- Azpiprozesuak hierarkia honetako elementuetan sakonduko dut task-en mailara arte 
;	  rolak aparte geratzen dira eta sekzioak sekuentzialki aztertuko dira:
;		- method_id: metodologia identifikatzeko zenbakia.
;		- phase_id: fasea identifikatzeko zenbakia.
;		- activity_id: aktibitatea identifikatzeko zenbakia.
;		- task_id: jarduera identifikatzeko zenbakia.
;			> role_id: rola identifikatzeko zenbakia.
;			> artifact_id: artefaktua identifikatzeko zenbakia.
;			> section_number: sekzioaren zenbakia (float)

; Workflow bateko deia....: "Methodology Phase 1 1 0 0 0 0 0 Inception |"   (daiearen ?erpina)  
; Azpiworkflowaren bukaera: "Phase Start 1 1 0 0 0 0 0 Inception |"   (bueltatuko den erpin izena)

; Workflow bateko deia....: "Activity Task 1 1 1 1 1 0 0 Define Vision | Analyst"
; Azpiworkflowaren bukaera:    "Task Start 1 1 1 1 1 0 0 Define Vision |"    (bueltatuko den erpin izena)


	(bind ?prozesuPosizioa		(member$ (nth$ 1 (explode$ ?erpina))  (create$ Methodology Phase Activity Task Artifact)))
	(bind ?azpiprozesuMota		(nth$ (+ ?prozesuPosizioa 1) (create$ Methodology Phase Activity Task Artifact))) 
	(bind ?erpinMota			(nth$ 2 (explode$ ?erpina)))
	(bind ?erpinKodeHasieraPos	(+  (str-index ?erpinMota ?erpina) (str-length ?erpinMota) 1))
	(bind ?erpinIzenBukaeraPos	(str-index "|" ?erpina))										
	
;	(printout t crlf ">erpina> " ?erpina "  >erpinMota> " ?erpinMota "  >erpinKodeHasieraPos> " ?erpinKodeHasieraPos  crlf)
	
	(return   (str-cat  ?erpinMota 
						" "
						Start						
						" "
						(sub-string   ?erpinKodeHasieraPos ?erpinIzenBukaeraPos ?erpina)
				))
	)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;	
 
 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;	

(defglobal
	?*log*  = null
	?*log-egin*  = TRUE
	?*allAssessmentIsCorrectInception*  = TRUE
	?*allAssessmentIsCorrectElaboration*  = TRUE
	?*allAssessmentIsCorrectConstruction*  = TRUE
	?*allAssessmentIsCorrectTransition*  = TRUE
	?*BDconnected* = 0
	?*BDdisconnected* = 0
	?*BisitaOrdena* = 0
	?*nodoenKontagailua* = 0
	?*atazenKontagailua* = 0
	?*bisitatuGabekoenKontagailua* = 0	
	?*DatuBaseaAldatu* = TRUE				;probak egin edo datu basea aldatu
	
	)
	
	
(deffacts hasierakoEgitateak
	(SoftwarearenBertsioIdentifikatzaileak  (create$ "Pre-alpha" "Alpha" "Beta" "Release candidate" "RTM" "GA" "Production"))
	;;Bertsioen identifikatzaileak (https://en.wikipedia.org/wiki/Software_release_life_cycle)  
	;;	- Proba edo garapen garaia:
	;;		- Pre-alpha edo development releases edo nightly builds
	;;		- Alpha
	;;		- Beta
	;;		- Release candidate edo gamma edo delta
	;;	- Azken bertsioen garaia:
	;;		- RTM edo Release to manufacturing edo release to marketing
	;;		- GA edo General availability
	;;		- Production or live release edo Gold

	
)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;	
	
	
(defrule DatuBaseraKonektatu
	(declare (salience 100))
		(ProcessDataDB 		?DBsource )					
=>
(if ?*DatuBaseaAldatu*
	then ;---------------------------------------------------------------------------------------- KONEXIOA 
		(bind ?*BDconnected*  (SQLConnect ?DBsource))

		(if (= ?*BDconnected* 0)
			then 
				(printout ?*log* crlf "Ez da konektatu " ?DBsource " datu basera " ?*BDconnected* " "(GetTime))
			else 
				(printout ?*log* crlf "Konektatu da " ?DBsource " datu basera " ?*BDconnected* " " (GetTime))
				(assert (Data base connected to ?DBsource ?*BDconnected*  ))
			)
	else
		(printout ?*log* crlf "Grafoa aztertua: Bisitatu gabekoak = " ?*bisitatuGabekoenKontagailua* " / "  ?*nodoenKontagailua* "  Bisitatutakoak = " (- ?*nodoenKontagailua* ?*bisitatuGabekoenKontagailua*))
		(assert (Data base connected to ?DBsource ?*BDconnected*  ))
		)
 ;----------------------------------------------------------------------------------------  
 )
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;	
	
(defrule Bukatu
	(declare (salience -100))
	(Data base connected to ?DBsource ?BDconnected)
=>
(printout ?*log* crlf "Grafoa aztertua: Bisitatu gabekoak = " ?*bisitatuGabekoenKontagailua* " / "  ?*nodoenKontagailua* "  Bisitatutakoak = " (- ?*nodoenKontagailua* ?*bisitatuGabekoenKontagailua*))
(printout ?*log* crlf "Grafoa aztertua: atazen kontagailua  = " ?*atazenKontagailua*)				

(printout ?*log* crlf "Datu basea grabatua")

(if ?*DatuBaseaAldatu*
	then ;---------------------------------------------------------------------------------------- KONEXIOA MOZTU

		(bind ?deskonektatua  (SQLDisconnect ?BDconnected))

		(if (= ?deskonektatua 0)
			then 
				(printout ?*log* crlf "Ez da deskonektatu " ?DBsource " datu basera " ?deskonektatua " "(GetTime))
			else 
				(printout ?*log* crlf "Deskonektatu da " ?DBsource " datu basera " ?deskonektatua " " (GetTime))
				(assert (Data base disconnected to ?DBsource ?deskonektatua  ))
				) 
	else
		(printout ?*log* crlf "Deskonektatu da " ?DBsource " datu basera " 0 " " (GetTime))
		(assert (Data base disconnected to ?DBsource 0  ))
		)			


(close ?*log*)
)

	

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;	
	
	
	
	
(defrule SakonerakoBilaketa  
	(declare (salience 40))
	(GrafoBatenHasierakoErpina  ?oraingoErpina)					
														;; Grafoak ez konektatuak direnez, grafo bakarra aztertuko du. 
														;; Haiseran "Methodology Start 1 0 0 0 0 0 0    1 OpenUp Lifecycle alpha 1" erpinetik abiatuko da 
														;; Gero, azpiprozesu bakoitzaren hasiaeratik sakonrako ordenean
	?erpin<-(object (is-a  node)
					(name 		?erpinObjektua)
					(_id 		?erpinIzena)             
					)	
		(test (eq (upcase ?oraingoErpina) (upcase ?erpinIzena)))   		;;Testuen idazkera okerra konparaketan gainditzeko 
		(test (eq (nth$ 2 (explode$ ?erpinIzena)) Start))
=>
(assert (GrafoBereanJarraitu  ?erpinIzena))		
(printout ?*log* crlf crlf "SakonerakoBilaketa: hemendik hasi= " ?erpinObjektua " = " ?erpinIzena )				;;
			
)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


(defrule AztertuGrafoaErpinBatetikHasita     
	;;"Erpin batetik (edozein) hasita atzigarriak diren erpinak aztertzen ditu"
	(declare (salience 31))
	?O<-(GrafoBereanJarraitu  ?oraingoErpina)
	?erpin<-(object (is-a  node)
					(name 		?erpinObjektua)
					(_id 		?erpinIzena)             
					)
		(test (eq (upcase ?oraingoErpina) (upcase ?erpinIzena)))   		;;Testuen idazkera okerra konparaketan gainditzeko 
		(not (Aztertuta  ?oraingoErpina	?bisita))					
	?ertza<-(object (is-a  edge)
				(_source 	?erpinIzena)						
				(_target  	?hurrengoErpina)									
				)	
	?erpin2<-(object (is-a  node)
					(name 		?erpinObjektua2)
					(_id 		?hurrengoErpina)             
					)				
	(test (WorkflowBerekoObjektuIzenak ?erpinObjektua2 ?erpinObjektua))
=>
(bind ?*BisitaOrdena* (+ ?*BisitaOrdena* 1))
(assert (Aztertuta  ?erpinIzena	?*BisitaOrdena*))

;; Bisita aurreko ekintzak ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(assert (Metodologiaren_KodeIzenBertsioaEskuratu ?erpinIzena))
(assert (Faseen_KodeIzenakEskuratu ?erpinIzena))
(assert (Jardueren_KodeIzenakEskuratu ?erpinIzena))
(assert (Jardueren_FaseKodeIzenakEskuratu ?erpinIzena))
(assert (Atazen_FaseJardueraRolKodeIzenakEskuratu ?erpinIzena))
(assert (Artefaktuen_FaseJardueraAtazaRolKodeIzenakEskuratu ?erpinIzena))
(assert (Sekzioen_FaseJardueraAtazaArtefaktuRolKodeIzenGidakEskuratu ?erpinIzena))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(assert  (GrafoBereanJarraitu  ?hurrengoErpina))
;(retract ?O)
(printout ?*log* crlf  crlf ?*BisitaOrdena* " ---------->> AztertuGrafoaEpinBatetikHasita: Aztertuta = " ?erpinObjektua " (" ?erpinIzena ")"   crlf "     -------------------->> Hurrengoa = " ?erpinObjektua2 " (" ?hurrengoErpina ") " )	
)	

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


(defrule AztertuGrafoaErpinBatetikHasitaEnd     
	;;"Erpin batetik (edozein) hasita atzigarriak diren erpinak aztertzen ditu"
	(declare (salience 31))
	?O<-(GrafoBereanJarraitu  ?oraingoErpina)
	?erpin<-(object (is-a  node)
					(name 		?erpinObjektua)
					(_id 		?erpinIzena)             
					)
		(test (eq (upcase ?oraingoErpina) (upcase ?erpinIzena)))   		;;Testuen idazkera okerra konparaketan gainditzeko 
		(not (Aztertuta  ?oraingoErpina	?bisita))					
	(not(object (is-a  edge)
				(name 		?erpinObjektua2&:(eq (grafoIzena ?erpinObjektua2) (grafoIzena ?erpinObjektua)))  ;;grafo berekoak dira
				(_source 	?erpinIzena)															
				))	
=>
(bind ?*BisitaOrdena* (+ ?*BisitaOrdena* 1))
(assert (Aztertuta  ?oraingoErpina	?*BisitaOrdena*))
;; Bisita aurreko ekintzak

(printout ?*log* crlf  crlf ?*BisitaOrdena* " |---------->> AztertuGrafoaEpinBatetikHasita: Aztertuta = " ?erpinObjektua " (" ?erpinIzena ") -----------||" )	
)	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defrule AztertuGrafoaErpinBatetikHasitaBesteBideBat     
	;;"Erpin batetik (edozein) hasita atzigarriak diren erpinak aztertzen ditu"
	(declare (salience 31))
	?O<-(GrafoBereanJarraitu  ?oraingoErpina)
	?erpin<-(object (is-a  node)
					(name 		?erpinObjektua)
					(_id 		?erpinIzena)             
					)
		(test (eq (upcase ?oraingoErpina) (upcase ?erpinIzena)))   		;;Testuen idazkera okerra konparaketan gainditzeko 
			(Aztertuta  ?erpinIzena	?bisita)				
	?ertza<-(object (is-a  edge)
				(_source 	?erpinIzena)						
				(_target  	?hurrengoErpina)									
				)	
	?erpin2<-(object (is-a  node)
					(name 		?erpinObjektua2)
					(_id 		?hurrengoErpina)             
					)				
			(not(Aztertuta  ?hurrengoErpina	?bisita))					
	(test (WorkflowBerekoObjektuIzenak ?erpinObjektua2 ?erpinObjektua))
=>
;; Bisita aurreko ekintzak ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(assert (Atazen_FaseJardueraRolKodeIzenakEskuratu ?erpinIzena))
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(assert  (GrafoBereanJarraitu  ?hurrengoErpina))

(printout ?*log* crlf  crlf ?*BisitaOrdena* " ############> AztertuGrafoaErpinBatetikHasitaBesteBideBat: Aztertuta = " ?erpinObjektua " (" ?erpinIzena ")" crlf "-------------------->> Hurrengoa = " ?erpinObjektua2 " (" ?hurrengoErpina ") " )	
)	


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defrule BesteGrafoBateanSakondu  					
	;; Zenbaki guztiak dituen ekintza batetik sakonduko da, azpiprozesuko lehenari (Start nodo bat) ere zenbakiak jarri
	(declare (salience 40))
		(GrafoBereanJarraitu  ?oraingoErpina)	
	?erpin<-(object (is-a  node)
					(name 		?erpinObjektua)
					(_id 		?erpinIzena)             
					)
		(test (eq (upcase ?oraingoErpina) (upcase ?erpinIzena)))   		;;Testuen idazkera okerra konparaketan gainditzeko 
		(test (member$ (nth$ 1 (explode$ ?erpinIzena))   (create$ Methodology Phase Activity Task Artifact) ))
		(test (member$ (nth$ 2 (explode$ ?erpinIzena))   (create$ Phase Activity Task Artifact Section SectionWT UploadGraph) ))
=>	

(bind 	?hurrengoaSakoneran	(DeiarenAzpiprozesua ?erpinIzena))		
(assert (GrafoBatenHasierakoErpina  ?hurrengoaSakoneran))			

(printout ?*log* crlf crlf "       \\\\\\\\\\\\\\>> BesteGrafoBateanSakondu: oraingoErpina = " ?erpinObjektua " (" ?erpinIzena ") "   crlf "        \\\\\\\\\\\\\\>> azpiprozesuko Lehena = "  ?hurrengoaSakoneran  )						
)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; BISITATUTKO ERPINEI DAGOZKIEN EKINTZAK  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defrule MeotodlogiarenInformazioaAtera  
	(Data base connected to ?DBsource ?BDconnected  )
	(Metodologiaren_KodeIzenBertsioaEskuratu ?erpinIzena)
	(not (Metodologia ?k ?i ?b))
	(test (eq (nth$ 1 (explode$ ?erpinIzena)) Methodology))
	(test (eq (nth$ 2 (explode$ ?erpinIzena)) Start))
	(SoftwarearenBertsioIdentifikatzaileak  $?bertsioIdentifikatzaileak)

=>
(bind ?metodologia 	(nth$ 3 (explode$ ?erpinIzena)))
(bind ?barraPos	(member$ "|" (explode$ ?erpinIzena)))
(bind ?izena	(implode$ (subseq$ (explode$ ?erpinIzena) 10 (- ?barraPos 1))))
(bind ?bertsioa	(implode$ (subseq$ (explode$ ?erpinIzena) (+ ?barraPos 1) (length$ (explode$ ?erpinIzena)))))

;; 	?bertsioIdentifikatzaileak  = (create$ Pre-alpha Alpha Beta Release-candidate RTM GA Production)
(if (not (numberp ?bertsioa))
	then	(bind ?bertsioZenbakia	(member$ ?bertsioa ?bertsioIdentifikatzaileak))    ;; FALSE izan daiteke
			(if  (eq ?bertsioZenbakia FALSE)  
				then	(bind ?bertsioZenbakia 	0))	
	else	(bind ?bertsioZenbakia 	?bertsioa))
;(assert (Metodologia ?metodologia ?izena ?bertsioZenbakia))

(if ?*DatuBaseaAldatu*
	then ;---------------------------------------------------------------------------------------- DATU BASERA
															;; id 	name 	version 	
		(bind 	?SqlInsertar  (eval (format nil "\"INSERT IGNORE INTO %s.methodologies (id, name, version) VALUES (%d, \'%s\', %d)\" " ?DBsource ?metodologia ?izena ?bertsioZenbakia))) 
		(printout ?*log* crlf ">> " ?SqlInsertar) 

		(bind ?insertado (SQLExecute ?*BDconnected* ?SqlInsertar)) 
		(if (= ?insertado 0)
			then 
				(printout ?*log* crlf "methodologies:No se ha insertado: " ?DBsource "," ?metodologia "," ?izena "," ?bertsioZenbakia )	
			else 
				(printout ?*log* crlf "methodologies: Se ha insertado: " ?DBsource "," ?metodologia "," ?izena "," ?bertsioZenbakia "	"(GetTime))	
			)    
	else ;----------------------------------------------------------------------------------------  
		(printout ?*log* crlf ">>>>>>>>> methodologies: " ?DBsource "," ?metodologia "," ?izena "," ?bertsioZenbakia "	"(GetTime))	
		)
)
;;--------------------------------------------------------------------------------------------------------------------------------------------------

(defrule FaseenInformazioaAtera  
	(Data base connected to ?DBsource ?BDconnected  )
	(Faseen_KodeIzenakEskuratu ?erpinIzena)
	(test (eq (nth$ 1 (explode$ ?erpinIzena)) Methodology))
	(test (eq (nth$ 2 (explode$ ?erpinIzena)) Phase))
=>
(bind ?metodologia 	(nth$ 3 (explode$ ?erpinIzena)))
(bind ?fasea 		(nth$ 4 (explode$ ?erpinIzena)))
(bind ?barraPos		(member$ "|" (explode$ ?erpinIzena)))
(bind ?faseIzena	(implode$ (subseq$ (explode$ ?erpinIzena) 10 (- ?barraPos 1))))
;(assert (FaseIzena ?fasea ?faseIzena))

(if ?*DatuBaseaAldatu*
	then ;---------------------------------------------------------------------------------------- DATU BASERA
																;; methodology_id 	id 	name 
		(bind 	?SqlInsertar  
				(eval (format nil "\"INSERT IGNORE INTO %s.phases (methodology_id, id, name) VALUES (%d, %d, \'%s\' )\" " ?DBsource ?metodologia ?fasea  ?faseIzena))) 
		(printout ?*log* crlf ">> " ?SqlInsertar) 

		(bind ?insertado (SQLExecute ?*BDconnected* ?SqlInsertar)) 
		(if (= ?insertado 0)
			then 
				(printout ?*log* crlf "phases:No se ha insertado: " ?DBsource ", " ?metodologia ", " ?fasea "," ?faseIzena)	
			else 
				(printout ?*log* crlf "phases: Se ha insertado: " ?DBsource ", " ?metodologia ", " ?fasea "," ?faseIzena "	"(GetTime))	
			)  
	else ;----------------------------------------------------------------------------------------  
		(printout ?*log* crlf ">>>>>>>>> phases:  " ?DBsource ", " ?metodologia ", " ?fasea "," ?faseIzena "	"(GetTime))
		)
)

;;--------------------------------------------------------------------------------------------------------------------------------------------------

(defrule JarduerenInformazioaAtera  
	(Data base connected to ?DBsource ?BDconnected  )
	(Jardueren_KodeIzenakEskuratu ?erpinIzena)
	(test (eq (nth$ 1 (explode$ ?erpinIzena)) Phase))
	(test (eq (nth$ 2 (explode$ ?erpinIzena)) Activity ))
=>
(bind ?metodologia 		(nth$ 3 (explode$ ?erpinIzena)))
(bind ?fasea 			(nth$ 4 (explode$ ?erpinIzena)))
(bind ?jarduera 		(nth$ 5 (explode$ ?erpinIzena)))
(bind ?barraPos			(member$ "|" (explode$ ?erpinIzena)))
(bind ?jardueraIzena	(implode$ (subseq$ (explode$ ?erpinIzena) 10 (- ?barraPos 1))))
;; (assert (JarduerenIzena ?fasea ?jarduera ?jardueraIzena))
(if ?*DatuBaseaAldatu*
	then ;---------------------------------------------------------------------------------------- DATU BASERA
															;; methodology_id 	phase_id 	id 	name 	
		(bind 	?SqlInsertar  
				(eval (format nil "\"INSERT IGNORE INTO %s.activities (methodology_id, phase_id, id, name) VALUES (%d, %d, %d, \'%s\' )\" " ?DBsource ?metodologia ?fasea ?jarduera ?jardueraIzena))) 
		(printout ?*log* crlf ">> " ?SqlInsertar) 

		(bind ?insertado (SQLExecute ?*BDconnected* ?SqlInsertar)) 
		(if (= ?insertado 0)
			then 
				(printout ?*log* crlf "activities:No se ha insertado: " ?DBsource ", " ?metodologia ", " ?fasea ", " ?jarduera ", " ?jardueraIzena)	
			else 
				(printout ?*log* crlf "activities: Se ha insertado: " ?DBsource  ", " ?metodologia ", " ?fasea", " ?jarduera ", " ?jardueraIzena	 "	"(GetTime))	
			)
	else ;----------------------------------------------------------------------------------------  
		(printout ?*log* crlf ">>>>>>>>> activities:  " ?DBsource  ", " ?metodologia ", " ?fasea", " ?jarduera ", " ?jardueraIzena	 "	"(GetTime))	
		)			
)
;;--------------------------------------------------------------------------------------------------------------------------------------------------

(defrule AtazenInformazioaAtera  
	(Data base connected to ?DBsource ?BDconnected  )
	(Atazen_FaseJardueraRolKodeIzenakEskuratu ?erpinIzena)
	(test (eq (nth$ 1 (explode$ ?erpinIzena)) Activity))
	(test (eq (nth$ 2 (explode$ ?erpinIzena)) Task ))
=>
(bind ?metodologia 	(nth$ 3 (explode$ ?erpinIzena)))
(bind ?fasea 		(nth$ 4 (explode$ ?erpinIzena)))
(bind ?jarduera 	(nth$ 5 (explode$ ?erpinIzena)))
(bind ?ataza 		(nth$ 6 (explode$ ?erpinIzena)))
(bind ?rol 			(nth$ 7 (explode$ ?erpinIzena)))
(bind ?barraPos		(member$ "|" (explode$ ?erpinIzena)))
(bind ?atazaIzena	(implode$ (subseq$ (explode$ ?erpinIzena) 10 (- ?barraPos 1))))
(bind ?rolIzena		(implode$ (subseq$ (explode$ ?erpinIzena) (+ ?barraPos 1) (length$  (explode$ ?erpinIzena) ))))
;; (assert (AtazenIzena ?fasea ?jarduera ?ataza ?rol ?atazaIzena ?rolIzena))

(bind ?*atazenKontagailua* (+ ?*atazenKontagailua* 1))

(if ?*DatuBaseaAldatu*
	then ;---------------------------------------------------------------------------------------- DATU BASERA

												;;  	methodology_id 	phase_id 	activity_id 	id 	name 	role 	
		(bind 	?SqlInsertar  
				(eval (format nil "\"INSERT IGNORE INTO %s.tasks (methodology_id, phase_id, activity_id, id, name, role) VALUES (%d, %d, %d, %d, \'%s\', %d)\" " ?DBsource ?metodologia ?fasea ?jarduera ?ataza ?atazaIzena ?rol))) 
		(printout ?*log* crlf ">> " ?SqlInsertar) 

		(bind ?insertado (SQLExecute ?*BDconnected* ?SqlInsertar)) 
		(if (= ?insertado 0)
			then 
				(printout ?*log* crlf "tasks:No se ha insertado: " ?DBsource ", " ?metodologia ", " ?fasea ", " ?jarduera "," ?ataza "," ?atazaIzena "," ?rol )	
			else 
				(printout ?*log* crlf "tasks: Se ha insertado: " ?DBsource ", " ?metodologia ", " ?fasea ", " ?jarduera "," ?ataza "," ?atazaIzena "," ?rol "	"(GetTime))	
			)
														;;  methodology_id 	id 	name 	
		(bind 	?SqlInsertar  
				(eval (format nil "\"INSERT IGNORE INTO %s.roles (methodology_id, id, name) VALUES (%d, %d, \'%s\' )\" " ?DBsource ?metodologia ?rol ?rolIzena ))) 
		(printout ?*log* crlf ">> " ?SqlInsertar) 

		(bind ?insertado (SQLExecute ?*BDconnected* ?SqlInsertar)) 
		(if (= ?insertado 0)
			then 
				(printout ?*log* crlf "roles:No se ha insertado: " ?DBsource ", " ?metodologia "," ?rol ", " ?rolIzena )	
			else 
				(printout ?*log* crlf "roles: Se ha insertado: " ?DBsource ", " ?metodologia "," ?rol ", " ?rolIzena  "	"(GetTime))	
			)
	else ;----------------------------------------------------------------------------------------  
		(printout ?*log* crlf ">>>>>>>>> tasks:  " ?DBsource ", " ?metodologia ", " ?fasea ", " ?jarduera "," ?ataza "," ?atazaIzena "," ?rol "	"(GetTime))
		(printout ?*log* crlf ">>>>>>>>> roles:  " ?DBsource ", " ?metodologia "," ?rol ", " ?rolIzena  "	"(GetTime))
		)		
)










;; (implode$ (subseq$ (explode$ "Activity Task 1 1 2 1 5 0 0 Plan Iteration | Project Manager") 13 (length$ (explode$ "Activity Task 1 1 2 1 5 0 0 Plan Iteration | Project Manager"))))
;; (nth$ 13 (explode$ "Activity Task 1 1 2 1 5 0 0 Plan Iteration | Project Manager"))   => Project
;; (nth$ 12 (explode$ "Activity Task 1 1 2 1 5 0 0 Plan Iteration | Project Manager"))   => Project
;;--------------------------------------------------------------------------------------------------------------------------------------------------


(defrule ArtefaktuenInformazioaAtera  
	(Data base connected to ?DBsource ?BDconnected  )
	(Artefaktuen_FaseJardueraAtazaRolKodeIzenakEskuratu ?erpinIzena)
	(test (eq (nth$ 1 (explode$ ?erpinIzena)) Task))
	(test (eq (nth$ 2 (explode$ ?erpinIzena)) Artifact ))
=>
(bind ?metodologia 		(nth$ 3 (explode$ ?erpinIzena)))
(bind ?fasea 			(nth$ 4 (explode$ ?erpinIzena)))
(bind ?jarduera 		(nth$ 5 (explode$ ?erpinIzena)))
(bind ?ataza 			(nth$ 6 (explode$ ?erpinIzena)))
(bind ?rol 				(nth$ 7 (explode$ ?erpinIzena)))
(bind ?artefaktua 		(nth$ 8 (explode$ ?erpinIzena)))
(bind ?barraPos			(member$ "|" (explode$ ?erpinIzena)))
(bind ?artefaktuIzena	(implode$ (subseq$ (explode$ ?erpinIzena) 10 (- ?barraPos 1))))
;;(assert (ArtefaktuIzena ?fasea ?jarduera ?ataza ?rol ?artefaktua ?artefaktuIzena))
(if ?*DatuBaseaAldatu*
	then ;---------------------------------------------------------------------------------------- DATU BASERA
												;;; methodology_id 	id 	name 	
		(bind 	?SqlInsertar  
				(eval (format nil "\"INSERT IGNORE INTO %s.artifacts (methodology_id, id, name) VALUES (%d, %d, \'%s\')\" " ?DBsource ?metodologia ?artefaktua ?artefaktuIzena )))
		(printout ?*log* crlf ">> " ?SqlInsertar) 

		(bind ?insertado (SQLExecute ?*BDconnected* ?SqlInsertar)) 
		(if (= ?insertado 0)
			then 
				(printout ?*log* crlf "artifacts:No se ha insertado: " ?DBsource "," ?metodologia "," ?artefaktua", " ?artefaktuIzena )	
			else 
				(printout ?*log* crlf "artifacts: Se ha insertado: " ?DBsource "," ?metodologia "," ?artefaktua", " ?artefaktuIzena "	"(GetTime))	
			)
	else ;----------------------------------------------------------------------------------------  
		(printout ?*log* crlf ">>>>>>>>> artifacts:  " ?DBsource "," ?metodologia "," ?artefaktua", " ?artefaktuIzena "	"(GetTime))
		)		
)

;;--------------------------------------------------------------------------------------------------------------------------------------------------


(defrule TxantiloidunSekzioenInformazioaAtera  
	(Data base connected to ?DBsource ?BDconnected  )
	(Sekzioen_FaseJardueraAtazaArtefaktuRolKodeIzenGidakEskuratu ?erpinIzena)
	(test (eq (nth$ 1 (explode$ ?erpinIzena)) Artifact))
	(test (eq (nth$ 2 (explode$ ?erpinIzena)) SectionWT))
=>
(bind ?metodologia 	(nth$ 3 (explode$ ?erpinIzena)))
(bind ?fasea 		(nth$ 4 (explode$ ?erpinIzena)))
(bind ?jarduera 	(nth$ 5 (explode$ ?erpinIzena)))
(bind ?ataza 		(nth$ 6 (explode$ ?erpinIzena)))
(bind ?rol 			(nth$ 7 (explode$ ?erpinIzena)))
(bind ?artefaktua 	(nth$ 8 (explode$ ?erpinIzena)))
(bind ?sekzioa 		(nth$ 9 (explode$ ?erpinIzena)))
(bind ?barraPos		(member$ "|" (explode$ ?erpinIzena)))
(bind ?sekzioIzena	(implode$ (subseq$ (explode$ ?erpinIzena) 10 (- ?barraPos 1))))
(bind ?gida			(nth$ (+ ?barraPos 1) (explode$ ?erpinIzena)))
;;(assert (TxantiloidunArtefktuZekzioIzena ?fasea ?jarduera ?ataza ?rol ?artefaktua ?sekzioa ?sekzioIzena ?gida))
(if ?*DatuBaseaAldatu*
	then ;---------------------------------------------------------------------------------------- DATU BASERA
												;; methodology_id 	artifact_id 	section_number 	section_name 	section_guide 	
		(bind 	?SqlInsertar  
				(eval (format nil "\"INSERT IGNORE INTO %s.artifact_sections (methodology_id, artifact_id, section_number, section_name, section_guide) VALUES (%d, %d, %d, \'%s\', \'%s\' )\" " ?DBsource ?metodologia ?artefaktua ?sekzioa ?sekzioIzena ?gida )))
		(printout ?*log* crlf ">> " ?SqlInsertar) 

		(bind ?insertado (SQLExecute ?*BDconnected* ?SqlInsertar)) 
		(if (= ?insertado 0)
			then 
				(printout ?*log* crlf "artifact_sections:No se ha insertado: " ?DBsource "," ?metodologia "," ?artefaktua "," ?sekzioa "," ?sekzioIzena "," ?gida )	
			else 
				(printout ?*log* crlf "artifact_sections: Se ha insertado: " ?DBsource "," ?metodologia "," ?artefaktua "," ?sekzioa "," ?sekzioIzena "," ?gida "	"(GetTime))	
			)
	else ;----------------------------------------------------------------------------------------  
		(printout ?*log* crlf ">>>>>>>>> artifact_sections: " ?DBsource "," ?metodologia "," ?artefaktua "," ?sekzioa "," ?sekzioIzena "," ?gida "	"(GetTime))	
		)	
			
)

;;--------------------------------------------------------------------------------------------------------------------------------------------------


(defrule TxantiloiGabekoSekzioenInformazioaAtera  
	(Data base connected to ?DBsource ?BDconnected  )
	(Sekzioen_FaseJardueraAtazaArtefaktuRolKodeIzenGidakEskuratu ?erpinIzena)
	(test (eq (nth$ 1 (explode$ ?erpinIzena)) Artifact))
	(test (eq (nth$ 2 (explode$ ?erpinIzena)) Section))
=>
(bind ?metodologia 	(nth$ 3 (explode$ ?erpinIzena)))
(bind ?fasea 		(nth$ 4 (explode$ ?erpinIzena)))
(bind ?jarduera 	(nth$ 5 (explode$ ?erpinIzena)))
(bind ?ataza 		(nth$ 6 (explode$ ?erpinIzena)))
(bind ?rol 			(nth$ 7 (explode$ ?erpinIzena)))
(bind ?artefaktua 	(nth$ 8 (explode$ ?erpinIzena)))
(bind ?sekzioa 		(nth$ 9 (explode$ ?erpinIzena)))
(bind ?barraPos		(member$ "|" (explode$ ?erpinIzena)))
(bind ?sekzioIzena	(implode$ (subseq$ (explode$ ?erpinIzena) 10 (- ?barraPos 1))))
(bind ?gida			(nth$ (+ ?barraPos 1) (explode$ ?erpinIzena)))
;; (assert (TxantiloiGabekoArtefktuZekzioIzena ?fasea ?jarduera ?ataza ?rol ?artefaktua ?sekzioa ?sekzioIzena))
(if ?*DatuBaseaAldatu*
	then ;---------------------------------------------------------------------------------------- DATU BASERA
												;; methodology_id 	artifact_id 	section_number 	section_name 	section_guide 	
		(bind 	?SqlInsertar  
				(eval (format nil "\"INSERT IGNORE INTO %s.artifact_sections (methodology_id, artifact_id, section_number, section_name, section_guide) VALUES (%d, %d, %d, \'%s\', \'%s\' )\" " ?DBsource ?metodologia ?artefaktua ?sekzioa ?sekzioIzena ?gida )))
		(printout ?*log* crlf ">> " ?SqlInsertar) 

		(bind ?insertado (SQLExecute ?*BDconnected* ?SqlInsertar)) 
		(if (= ?insertado 0)
			then 
				(printout ?*log* crlf "artifact_sections:No se ha insertado: " ?DBsource "," ?metodologia "," ?artefaktua "," ?sekzioa "," ?sekzioIzena "," ?gida )	
			else 
				(printout ?*log* crlf "artifact_sections: Se ha insertado: " ?DBsource "," ?metodologia "," ?artefaktua "," ?sekzioa "," ?sekzioIzena "," ?gida "	"(GetTime))	
			)
	else ;----------------------------------------------------------------------------------------  
		(printout ?*log* crlf ">>>>>>>>> artifact_sections: " ?DBsource "," ?metodologia "," ?artefaktua "," ?sekzioa "," ?sekzioIzena "," ?gida "	"(GetTime))	
		)	

)

;; 
;; (nth$ 12 (explode$ "Artifact Section 1 1 3 1 1 12 8 Post-conditions |"))  >> nil

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; SAKONERAKO ALGORITMOAREN FUNTZIONAMENDUA AZTERTZEKO METRIKAK ;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

 

(defrule NodoakZenbatu  
	(declare (salience -1))
	?erpin<-(object (is-a  node)
					(name 		?erpinObjektua)
					(_id 		?oraingoErpina)             
					)
	(test (member$ (nth$ 1 (explode$ ?oraingoErpina))   (create$ Methodology Phase Activity Task Artifact) ))					
					
=>
(bind ?*nodoenKontagailua* (+ ?*nodoenKontagailua* 1))
)      

(defrule GrafoaAztertuta  
	(declare (salience -1))
	?erpin<-(object (is-a  node)
					(name 		?erpinObjektua)
					(_id 		?oraingoErpina)             
					)	
	?ertza1<-(object (is-a  edge)
				(name 		?ertzObjektua)
				(_source 	?aurrekoErpina)						
				(_target  	?oraingoErpina)									
				)	
	?ertza2<-(object (is-a  edge)
				(name 		?ertzObjektua2)
				(_source 	?oraingoErpina)						
				(_target  	?hurrengoErpina)									
				)
	?erpin3<-(object (is-a  node)
					(name 		?erpinObjektua3)
					(_id 		?aurrekoErpina)             
					)	
	?erpin4<-(object (is-a  node)
					(name 		?erpinObjektua4)
					(_id 		?hurrengoErpina)             
					)	
				
	(test (member$ (nth$ 1 (explode$ ?oraingoErpina))   (create$ Methodology Phase Activity Task Artifact) ))					
	(not (Aztertuta  ?oraingoErpina	?bisita))
=>
; Bisita ondorengo ekintzak
(bind ?*bisitatuGabekoenKontagailua* (+ ?*bisitatuGabekoenKontagailua* 1))
(printout t crlf "Aurrekoa.-------------------------------------- "  ?erpinObjektua3 " - " ?aurrekoErpina  crlf )
(printout t crlf "Aztertu gabekoa: " ?erpinObjektua " - " ?oraingoErpina crlf)
(printout t crlf "Hurrengoa-------------------------------------- "  ?erpinObjektua4 " - " ?hurrengoErpina crlf crlf)
)      

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defrule BilatuDeitzailea
	(declare (salience -10))
	?erpin<-(object (is-a  node)
					(name 		?erpinObjektua)
					(_id 		?erpinIzena)             
					)
		(test (eq (nth$ 2 (explode$ ?erpinIzena)) Start))
		(not(Aztertuta  ?erpinIzena	?bisita))			
=>
(printout t crlf "Aztertu gabekoa START: " ?erpinObjektua " - " ?erpinIzena crlf)
)



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

